plugins {
    id "base"
    id "com.github.node-gradle.node" version "7.1.0"
    id "org.openapi.generator" version "7.9.0"
}

node {
    /*
    must be false, because otherwise node and npm are downloaded and, I dont know why, the 'npm-cli.js' from
    inside the node_modules folder is executed which is far outdated.
    If this is false, the globally available node/npm is used.
     */
    download = false

    version = '22.3.0'
    npmVersion = '10.8.1'
}

tasks.register("cleanNodeModules", Delete) {
    delete 'node_modules'
    delete '.gradle'
}
clean.dependsOn 'cleanNodeModules'

tasks.register('npmBuild', NpmTask) {
    dependsOn npmInstall
    dependsOn tasks.openApiGenerate

    inputs.file file("${projectDir}/package.json")
    inputs.file file("${projectDir}/package-lock.json")
    inputs.dir file("${projectDir}/src/")
    inputs.files(fileTree("${projectDir}/node_modules") {
        exclude ".cache"
        exclude ".bin"
    })

    outputs.dir file("${projectDir}/build/dist")

    args = ['run', 'build']
}

assemble.dependsOn 'npmBuild'

tasks.register('npmStartGUI', NpmTask) {
    dependsOn npmInstall
    args = ['run', 'dev']
}

openApiGenerate {
    generatorName = 'typescript-axios'
    inputSpec = layout.projectDirectory.dir("..").dir("spec").file("openapi-spec.yml").asFile.path
    outputDir = layout.buildDirectory.dir("generated-ts/api").get().asFile.path
    configOptions = [
            "supportsES6"   : "true",
            "withInterfaces": "true"
    ]
}
